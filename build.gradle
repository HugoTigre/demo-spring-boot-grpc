plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'

    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "org.jetbrains.kotlin.kapt" version "1.3.21"
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.21'

    id "org.jetbrains.gradle.plugin.idea-ext" version "0.5"

    id 'com.palantir.jacoco-coverage' version '0.4.0'
    id "com.palantir.jacoco-full-report" version "0.4.0"

    id 'com.google.protobuf' version '0.8.8'
    id "java"
}

ext {
    jUnit = '5.4.1'
    mockito = '2.23.4'
}

group = 'com.pakybytes'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // Admin
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'

    // Web
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation('org.eclipse.jetty:jetty-webapp:9.4.14.v20181114')

    // UI
    implementation 'org.springframework.boot:spring-boot-starter-mustache'

    // Json
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8')

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    kapt("org.springframework.boot:spring-boot-configuration-processor")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1")

    // gRPC
    implementation 'io.grpc:grpc-netty:1.21.0'
    implementation 'io.grpc:grpc-protobuf:1.21.0'
    implementation 'io.grpc:grpc-stub:1.21.0'
    implementation 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    implementation 'com.google.protobuf:protobuf-java:3.8.0'

//    protobuf files("src/main/java/com/pakybytes/demo/springbootgrpc/api/protobuf/")

    // Tests
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude module: 'junit'
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api:$jUnit")
    testCompile("org.junit.jupiter:junit-jupiter-engine:$jUnit")

    testCompile("org.mockito:mockito-core:$mockito")
    testCompile("org.mockito:mockito-junit-jupiter:$mockito")
    testCompile('com.nhaarman:mockito-kotlin:1.6.0')

//    testProtobuf files("src/test/java/com/pakybytes/demo/springbootgrpc/api/protobuf/")
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}

//
protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
//    plugins {
//        // Optional: an artifact spec for a protoc plugin, with "grpc" as
//        // the identifier, which can be referred to in the "plugins"
//        // container of the "generateProtoTasks" closure.
//        grpc {
//            artifact = 'io.grpc:protoc-gen-grpc-java:1.21.0'
//        }
//    }
//    generateProtoTasks {
//        ofSourceSet('main')*.plugins {
//            // Apply the "grpc" plugin whose spec is defined above, without
//            // options.  Note the braces cannot be omitted, otherwise the
//            // plugin will not be added. This is because of the implicit way
//            // NamedDomainObjectContainer binds the methods.
//            grpc { }
//        }
//    }
}

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir 'src/main/java/com/pakybytes/demo/springbootgrpc/api/protobuf/'
            // In addition to the default '**/*.proto' (use with caution).
            // Using an extension other than 'proto' is NOT recommended,
            // because when proto files are published along with class files, we can
            // only tell the type of a file from its extension.
            include '**/*.protodev'
        }
    }
    test {
        proto {
            // In addition to the default 'src/test/proto'
            srcDir 'src/test/java/com/pakybytes/demo/springbootgrpc/api/protobuf/'
        }
    }
}


ext['junit-jupiter.version'] = jUnit // override spring dependency management

test {
    useJUnitPlatform() {
        excludeTags 'integration'
    }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        // outputs.upToDateWhen {false}
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags 'integration'
    }
    check.dependsOn it
    shouldRunAfter test
}


jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    
    afterEvaluate {
        getClassDirectories().setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: ['**/Main**'])
                })
        )
    }
}